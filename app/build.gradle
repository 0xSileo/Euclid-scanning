apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.tananaev.passportreader"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 2
        versionName "1.1"
        multiDexEnabled = true
    }
    signingConfigs {
        release {
            storeFile file('../../android.keystore')
            keyAlias 'tananaev'
            storePassword ''
            keyPassword ''
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
}

dependencies {
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'org.jmrtd:jmrtd:0.5.5'
    compile 'net.sf.scuba:scuba-sc-android:0.0.9'
    compile 'com.madgag.spongycastle:prov:1.54.0.0'
    compile 'edu.ucar:jj2000:5.2'
    compile 'com.github.mhshams:jnbis:1.1.0'
}

task requestPasswords << {
    android.signingConfigs.release.storePassword = new String(System.console().readPassword('\nStore password: '))
    android.signingConfigs.release.keyPassword = new String(System.console().readPassword('Key password: '))
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals('packageRelease')) {
        theTask.dependsOn 'requestPasswords'
    }
}
